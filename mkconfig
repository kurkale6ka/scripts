#! /usr/bin/env zsh

setopt extended_glob

# Don't update my cd bookmarks for automated cds
chpwd_functions=()

mkrepos() {

   mkdir -p ~/github

   if cd ~/github
   then
      git clone git@github.com:kurkale6ka/bash.git
      git clone git@github.com:kurkale6ka/config.git
      git clone git@github.com:kurkale6ka/help.git
      git clone git@github.com:kurkale6ka/zsh.git
      git clone git@github.com:kurkale6ka/scripts.git
      git clone git@github.com:kurkale6ka/vim.git
      if cd vim
      then
         git submodule init && git submodule update
      fi
      mktags
   fi
}

updaterepos() {

   for repo in ~/github/*(/)
   do
      if cd $repo
      then
         git fetch -q
         if [[ $(git symbolic-ref --short HEAD) == master ]] && git status -sb | grep -q behind
         then
            print -nP "%F{45}${repo:t}%f: "
            if git pull
            then
               git submodule sync >/dev/null && git submodule update
            fi
         fi
      fi
   done
}

mktags() {

   cd ~/github &&
   ctags -R                                \
   --langmap=vim:+.vimrc,zsh:+.zshrc_after \
   --exclude='*~ '                         \
   --exclude='.*~'                         \
   --exclude=bundle                        \
   --exclude=colors                        \
   --exclude=keymap                        \
   --exclude=pathogen.vim                  \
   ~/.zshrc_after                          \
   ~/github/zsh/.zsh/autoload              \
   ~/github/bash/scripts                   \
   ~/github/scripts                        \
   ~/github/vim                            \
   ~/github/vim/bundle/vsearch             \
   ~/github/vim/.bundle/blanklines         \
   ~/github/vim/.bundle/blockinsert        \
   ~/github/vim/.bundle/pairs              \
   ~/github/vim/.bundle/sequence           \
   ~/github/vim/.bundle/swap
}

bash=(.profile .bash_{profile,login,logout} .bashrc .logout)
configs=(.dir_colors .gitignore .{input,irb}rc .Xresources)
exes=(colors_term.bash colors_tmux.bash mkconfig)

mklinks() {

   # Create repo links in ~
   for repo in ~/github/*(/)
   do
      ln -s $repo ~
   done

   # Vim
   ${commands[gln]:-ln} -sT ~/github/vim ~/.vim
   ${commands[gln]:-ln} -sT ~/github/vim ~/.config/nvim
   ln -s ~/github/vim/.{,g}vimrc ~

   # Zsh
   ln -s ~/github/zsh/.zsh ~
   ln -s ~/github/zsh/.zprofile ~
   ln -s ~/github/zsh/.zshrc ~

   # Bash
   for c in $bash; ln -s ~/github/bash/$c ~
   ln -s ~/github/bash/scripts/cd/.cdmarks.skel ~

   mkdir -p ~/.config/ranger

   # Misc configs
   ln -s ~/github/config/ctags/.ctags ~
   ln -s ~/github/config/tmux/.tmux.conf ~
   ln -s ~/github/config/ranger/rc.conf ~/.config/ranger
   for c in $configs; ln -s ~/github/config/dotfiles/$c ~

   # ~/bin
   mkdir -p ~/bin
   if [[ -d ~/bin ]]
   then
      for c in $exes; ln -s ~/github/scripts/$c ~/bin

      ln -s ~/github/vim/scripts/vc ~/bin
      ln -s ~/github/config/tmux/lay ~/bin
   fi

} 2>/dev/null

rmlinks() {

   # Remove repo links in ~
   for l in ~/github/*(/:t)
   do
      'rm' ~/$l
   done

   # Vim
   'rm' ~/.vim ~/.config/nvim
   'rm' ~/.{,g}vimrc

   # Zsh
   'rm' ~/.zsh
   'rm' ~/.zprofile
   'rm' ~/.zshrc

   # Bash
   for c in $bash; 'rm' ~/$c
   'rm' ~/.cdmarks.skel

   # Misc configs
   'rm' ~/{.ctags,.tmux.conf}
   'rm' ~/.config/ranger/rc.conf
   for c in $configs; 'rm' ~/$c

   # ~/bin
   for c in $exes; 'rm' ~/bin/$c
   'rm' ~/bin/vc
   'rm' ~/bin/lay
}

opts[1]='Update repositories'
opts[2]='Create repositories'
opts[3]='Generate tags'
opts[4]='Make links'
opts[5]='Remove links'

select choice in $opts
do
   case $choice in
      (${opts[1]}) updaterepos;      break;;
      (${opts[2]}) mkrepos; mklinks; break;;
      (${opts[3]}) mktags;           break;;
      (${opts[4]}) mklinks;          break;;
      (${opts[5]}) rmlinks;          break;;
               (*) echo '*** Wrong choice ***' >&2
   esac
done

# vim: foldmethod=indent
